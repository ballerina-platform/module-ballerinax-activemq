/*
 * Copyright (c) 2025, WSO2 LLC. (http://www.wso2.com)
 *
 * WSO2 LLC. licenses this file to you under the Apache License,
 * Version 2.0 (the "License"); you may not use this file except
 * in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied. See the License for the
 * specific language governing permissions and limitations
 * under the License.
 */

import org.apache.tools.ant.taskdefs.condition.Os

plugins {
    id 'io.ballerina.plugin'
}

description = 'Ballerina - ActiveMQ Ballerina Connector'

def packageName = "activemq"
def packageOrg = "ballerinax"
def tomlVersion = stripBallerinaExtensionVersion("${project.version}")
def ballerinaTomlFilePlaceHolder = new File("${project.rootDir}/build-config/resources/Ballerina.toml")
def ballerinaTomlFile = new File("$project.projectDir/Ballerina.toml")

def stripBallerinaExtensionVersion(String extVersion) {
    if (extVersion.matches(project.ext.timestampedVersionRegex)) {
        def splitVersion = extVersion.split('-')
        if (splitVersion.length > 3) {
            def strippedValues = splitVersion[0..-4]
            return strippedValues.join('-')
        } else {
            return extVersion
        }
    } else {
        return extVersion.replace("${project.ext.snapshotVersion}", "")
    }
}

ballerina {
    packageOrganization = packageOrg
    module = packageName
    testCoverageParam = "--code-coverage --coverage-format=xml"
    isConnector = true
    platform = "java21"
}

configurations {
    externalJars
}

dependencies {
    /* ActiveMQ dependencies */
    externalJars(group: 'org.apache.activemq', name: 'activemq-client', version: "${activeMQVersion}") {
        transitive = true
    }

    /* JMS dependencies */
    externalJars(group: 'jakarta.jms', name: 'jakarta.jms-api', version: "${jmsVersion}") {
        transitive = false
    }
}

tasks.register('updateTomlFiles') {
    doLast {
        def newBallerinaToml = ballerinaTomlFilePlaceHolder.text.replace("@project.version@", project.version)
        newBallerinaToml = newBallerinaToml.replace("@toml.version@", tomlVersion)
        newBallerinaToml = newBallerinaToml.replace("@activemq.version@", activeMQVersion)
        newBallerinaToml = newBallerinaToml.replace("@jms.version@", jmsVersion)
        newBallerinaToml = newBallerinaToml.replace("@hawtbuf.version@", hawtbufVersion)
        ballerinaTomlFile.text = newBallerinaToml
    }
}

tasks.register('commitTomlFiles') {
    doLast {
        project.exec {
            ignoreExitValue true
            if (Os.isFamily(Os.FAMILY_WINDOWS)) {
                commandLine 'cmd', '/c', "git commit -m \"[Automated] Update the toml files\" Ballerina.toml Dependencies.toml"
            } else {
                commandLine 'sh', '-c', "git commit -m '[Automated] Update the toml files' Ballerina.toml Dependencies.toml"
            }
        }
    }
}

tasks.register('startActiveMQServer') {
    doLast {
        if (!Os.isFamily(Os.FAMILY_WINDOWS)) {
            def stdOut = new ByteArrayOutputStream()
            exec {
                commandLine 'sh', '-c', "docker ps --filter name=activemq-test-server"
                standardOutput = stdOut
            }
            if (!stdOut.toString().contains("activemq-test-server")) {
                println "Starting ActiveMQ server and waiting for it to be healthy..."
                exec {
                    commandLine 'sh', '-c', "docker compose -f tests/resources/docker-compose.yaml up --wait"
                    standardOutput = stdOut
                }
                println "ActiveMQ server is ready."
            } else {
                println "ActiveMQ server is already running."
            }
        }
    }
}

tasks.register('stopActiveMQServer') {
    doLast {
        if (!Os.isFamily(Os.FAMILY_WINDOWS)) {
            def stdOut = new ByteArrayOutputStream()
            exec {
                commandLine 'sh', '-c', "docker ps --filter name=activemq-test-server"
                standardOutput = stdOut
            }
            if (stdOut.toString().contains("activemq-test-server")) {
                println "Stopping ActiveMQ server."
                exec {
                    commandLine 'sh', '-c', "docker compose -f tests/resources/docker-compose.yaml down"
                    standardOutput = stdOut
                }
                println stdOut.toString()
            } else {
                println "ActiveMQ server is not started."
            }
        }
    }
}

build.dependsOn copyToLib
build.dependsOn ":${packageName}-native:build"

test.dependsOn ":${packageName}-native:build"
test.dependsOn "startActiveMQServer"
test.finalizedBy "stopActiveMQServer"

publish.dependsOn build
publishToMavenLocal.dependsOn build
